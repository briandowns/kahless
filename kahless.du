import Datetime;
import Sqlite;


const
    selectQuery = "SELECT {} FROM {}",
    insertQuery = "INSERT INTO {} (",
    updateQuery = "UPDATE {} (",
    createQuery = "CREATE TABLE IF NOT EXISTS {} (",
    deleteQuery = "DELETE FROM {} WHERE";

const
    singleQuote = "\"",
    spaceSeperator = " ";

const dictuToSQLType = {
    "string": "TEXT",
    "number": "INTEGER",
    "bool": "REAL",
};

// Config contains fieldss to provide when instantiating
// a new instance of Kahless.
class Config { 
    var debug = false;

    init(driver, path) {
        this.driver = driver;
        this.path = path;
    }

    setDebug(debug) {
        this.debug = debug;
    }
}

// Result holds the information associated with the 
// previously ran query.
class Result {
    var result = nil;
    var rowsAffected = 0;
    var error = nil;

    init() {}
}

// Model is a basic class that includes the following 
// fields: id, created_at, updated_at, deleted_at.
//
// To use this model in your model, have your class
// inherit this class.
//
// Models need to be defined with their zero value.
class Model {
    @PrimaryKey
    @AutoIncrement
    @Type("INTEGER")
    var id = 0;

    @Type("DATETIME")
    @Column("created_at")
    var createdAt = "";

    @Type("DATETIME")
    @Column("updated_at")
    var updatedAt = "";

    @Type("DATETIME")
    @Column("deleted_at")
    var deletedAt = "";
}

// isModelField checks if the given field is
// a field in the Model class.
def isModelField(field) {
    return ["id", "createdAt", "updatedAt", "deletedAt"].contains(field);
}

// Validation contains common logic used throughout the 
// Kahless module. It is to be inhereted into any class
// needing access to the logic.
class Validation {
    verifyAnnotations(model, name, required) {
        if (model._class.classAnnotations == nil) {
            return Error("class annotation missing from {}".format(model._name));
        }

        if (not model._class.classAnnotations.exists(name)) {
            return Error("'{}' annotation must exist on the given model".format(name));
        }
        
        return Success(nil);
    }
}

// Migrate is used to create tables in the database 
// for the given model.
class Migrate < Validation {
    init(var db) {}

    // run performs the migration.
    run(model) {
        const query = this.buildQuery(model).match(
            def(query) => query,
            def(error) => {
                return error;
            }
        );

        return this.db.db.execute(query);
    }

    // buildQuery builds the SQL query to create the table
    // for the given model.
    private buildQuery(model) {
        const klass = model._class;
        const modelName = model._class._name;

        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = klass.classAnnotations["Table"];

        var query = createQuery;
        query = query.format(tableName);
        
        const fieldAnnotations = klass.fieldAnnotations;
        const fields = fieldAnnotations.keys();
        
        for (var i = 0; i < fields.len(); i += 1) {
            if (fieldAnnotations.get(fields[i]).exists("Column")) {
                query += fieldAnnotations.get(fields[i]).get("Column");
            } else {
                query += fields[i];
            }
            
            if (fieldAnnotations.get(fields[i]).exists("Type")) {
                const fieldType = fieldAnnotations.get(fields[i]).get("Type");
                if (fieldType == "DATETIME" and fields[i] == "createdAt") {
                    query += spaceSeperator + "DATETIME DEFAULT CURRENT_TIMESTAMP";
                } else {
                    query += spaceSeperator + fieldAnnotations.get(fields[i]).get("Type");
                }
            } else {
                query += spaceSeperator + dictuToSQLType.get(type(fields[i]));
            }

            if (fieldAnnotations.get(fields[i]).exists("PrimaryKey")) {
                query += " PRIMARY KEY";
            }

            if (fieldAnnotations.get(fields[i]).exists("AutoIncrement")) {
                query += " AUTOINCREMENT";
            }

            if (i != fields.len()-1) {
                query += ", ";
            } else {
                query += ")";
            }
        }
        
        return Success(query);
    }
}

// Kahless is the primary provider of services for the module. All
// connections, queries, and validations are done via this class.
class Kahless < Validation {
    init(config) {
        this.config = config;
        
        this.connect();
    }

    private tableName(model) {
        return model._class.classAnnotations.get("Table");
    }

    // connect connects to the given database based on the driver
    // configured. Currently, only SQLite is supported.
    connect() {
        if (this.config.driver == "sqlite") {
            if (this.config.path == "") {
                this.db = Sqlite.connect(":memory:").unwrap();
            } else {
                this.db = Sqlite.connect(this.config.path).unwrap();
            }
        }
    }

    migrate(...models) {
        return models.map(def(m) => Migrate(this).run(m));
    }

    create(model) {
        const klass = model._class;
        const modelName = model._class._name;

        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = model._class.classAnnotations.get("Table");
        var query = insertQuery;

        query = query.format(tableName);

        const fieldAnnotations = klass.fieldAnnotations;
        const fields = fieldAnnotations.keys();

        var fieldsLen = fields.len();

        for (var i = 0; i < fields.len(); i += 1) {
            if (isModelField(fields[i])) {
                continue;
            }

            if (fieldAnnotations.get(fields[i]).exists("Column")) {
                query += fieldAnnotations.get(fields[i]).get("Column");
            } else {
                query += fields[i];
            }

            if (i != fields.len()-4) {
                query += ", ";
            } else {
                query += ")";
            }
        }

        query = query + " VALUES (";

        for (var i = 0; i < fieldsLen; i += 1) {
            if (isModelField(fields[i])) {
                continue;
            }

            var value = model.getAttribute(fields[i]);
            if (type(value) != "string") {
                value = value.toString();
            }
            query += singleQuote + value + singleQuote;

            if (i != fieldsLen-4) {
                query += ", ";
            } else {
                query += ")";
            }
        }

        return this.db.execute(query);
    }

    // rawQuery executes the given query with no checks
    // or validations and returns the results.
    rawQuery(query) {
        return this.db.execute(query);
    }

    // update
    update(model) {}

    // delete deletes the the record associated with the model
    // and id given.
    delete(model, v=nil) {
        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = this.tableName(model);
        var query = deleteQuery.format(tableName);

        const t = type(v);
        switch(t) {
            case "list": {
                if (v.len() == 0) {
                    return Error("invalid list of IDs");
                }

                const ids = v.toString().replace("[", "").replace("]", "");
                query += " id in ({})".format(ids);            
            }
            case "string": {
                if (v.toNumber() < 0) {
                    return Error("invalid ID");
                }

                query += " id = {}".format(id);
            }
            case "number": {
                if (v < 1) {
                    return Error("invalid ID");
                }

                query += " id = {}".format(v);
            }
            default:
                return Error("unsupported type: ", t);
        }

        return this.db.execute(query);
    }

    // first gets the first record ordered by primary key.
    first(model, id=-1) {
        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = this.tableName(model);

        var query = selectQuery.format("*", tableName);
        if (id > 1) {
            query += " WHERE id = {} ORDER BY id LIMIT 1".format(id);
        } else {
            query = selectQuery.format("*", tableName) + " ORDER BY id LIMIT 1";
        }
        
        const res = this.db.execute(query);
        if (not res.success()) {
            return res;
        }

        const r = Result();
        r.rows = res.unwrap();
        r.rowsAffected = res.unwrap().len();

        return Success(r); 
    }

    // take gets one record with no specified order.
    take(model) {
        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = this.tableName(model);
        const query = selectQuery.format("*", tableName) + " LIMIT 1";

        const res = this.db.execute(query);
        if (not res.success()) {
            return res;
        }

        const r = Result();
        r.rows = res.unwrap();
        r.rowsAffected = res.unwrap().len();

        return Success(r); 
    }

    // last gets the last record ordered by primary key desc.
    last(model) {
        const err = this.verifyAnnotations(model, "Table", true);
        if (not Success(err)) {
            return err;
        }

        const tableName = this.tableName(model);
        const query = selectQuery.format("*", tableName) + " ORDER BY id DESC LIMIT 1";

        const res = this.db.execute(query);
        if (not res.success()) {
            return res;
        }

        const r = Result();
        r.rows = res.unwrap();
        r.rowsAffected = res.unwrap().len();

        return Success(r);
    }
}
